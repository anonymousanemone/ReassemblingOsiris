"That Time I Got Out of Jail by Reassembling Osiris" by Sophia Ling and Ruya Tazebay

Include Basic Screen Effects by Emily Short.
Include Basic Help Menu by Emily Short.
Include Punctuation Removal by Emily Short.
Include Rideable Vehicles by Graham Nelson.
Include Secret Doors by Gavin Lambert.
Include Exit Lister by Gavin Lambert.

Include Vorple by Juhana Leinonen.
Include Vorple Command Prompt Control by Juhana Leinonen.
Include Vorple Hyperlinks by Juhana Leinonen.
Include Vorple Modal Windows by Juhana Leinonen.
Include Vorple Multimedia by Juhana Leinonen.
Include Vorple Screen Effects by Juhana Leinonen.
Include Vorple Status Line by Juhana Leinonen.
Include Vorple Tooltips by Juhana Leinonen.
 
Release along with the "Vorple" interpreter.
Release along with a file of "bg1" called "chariot.png".
Release along with a file of "map1" called "map.png".
Release along with a “Custom” website.

Hint is a Vorple style.
Notice is a Vorple style.
Scream is a Vorple style.


Release along with cover art ("Vase painting of Medea on her chariot").

Part 1 - Prologue - for release only

When play begins: 
	say "[scream style]YOU WILL BE PROMPTED TO SAVE A TRANSCRIPT.[end style]";
	pause the game;
	try switching the story transcript on;
	pause the game;
	say "[line break]'It’s a complete disaster!'[line break][line break]Amun let out a heavy sigh as he massaged his temples. The Emergency Meeting of the Pantheon was, once again, shaping up to be…utterly useless.[line break][line break]";
	wait for any key;
	say "I’ve begged and begged, but she refuses to leave her temples,' Nephthys continued, 'And why would she, when those Nubians make such splendid offerings? She’ll become an Osiris herself!'[line break][line break]";
    	wait for any key;
    	say "'I had thought,' an especially haggard-looking Anubis replied, 'that we agreed Isis would be no help. Have our previous Emergency Meetings taught us nothing? I am starting to hear whispers--mere whispers, mind you, but whispers nonetheless--about a certain Serapis. Serapis! If Osiris falls, Kemet will fall alongside him. Amun, you must do something!'[line break][line break]";
    	wait for any key;
	say "Amun ignored the whining. There was a more important matter: 'Maʽat, what have you heard from Thoth?'[line break][line break]";
	wait for any key;
	say "Maʽat looked rather unhappy to be the bearer of bad news. 'Thoth has tried every spell he knows, but to no effect. Now he’s sequestered himself in the library, but…'[line break][line break]";
	wait for any key;
	say "Ma'at seemed to have little faith that Thoth would succeed in his efforts.[line break][line break]";
    	wait for any key;
    	say "'More than that'--and her expression became grave--'Chaos is running rampant. I fear that if we do not take action soon, an irreversible disorder will conquer us all.'[line break][line break]";
    	wait for any key;
    	say "Indeed, this was a complete disaster. 'And what of Ptah?' Amun asked.[line break][line break]";
	wait for any key;
    	say "Silence rang in the room. [line break][line break]";
	wait for any key;
    	say "'...Has he made any progress in fashioning an Osiris?'[line break][line break]";
	wait for any key;
    	say "Sekhmet's snarl was answer enough.  [line break]'[line break]";
	wait for any key;
    	say "...I see,' Amun said. 'Perhaps…we should take a brief recess... and reconvene next week.' [line break][line break]";
	wait for any key;
    	say "This admittedly lackluster statement sent Anubis into a fury. 'This is the ninety-ninth Emergency Meeting we’ve held--enough stalling! The unweighed souls are piling up and spilling into the streets of the living, and poor Medjed is too spooked to perform his duties! I cannot contend with this alone!' [line break][line break]";
	wait for any key;
    	say "'And just what am I supposed to do, Anubis?' Amun spat back. 'Seth is rejoicing in his victory--clearly, none of us here have the aptitude to counter him!' [line break][line break]";
	wait for any key;
    	say "'If I may.' A quiet voice resounded from the back of the room, and everyone turned to look at Khonsu, who had yet to speak.[line break][line break]";
	wait for any key;
    	say "'I have hesitated to bring this up. But it might be our only hope. There have been murmurs, in the darkest and gloomiest of shadows, of a terrible woman who made the sky her own. A woman whose misdeeds put those of our Seth to shame. A woman whose pride and cleverness knew no bounds. A woman who had… [line break][line break]";
	wait for any key;
    	say "…[line break][line break]";
	wait for any key;
    	say "…I can’t bear to say it.'[line break][line break]";
	wait for any key;
    	say "'Well, go on. Say it!' [line break][line break]";
	wait for any key;
    	say "As the room broke out again into squabbling, Amun felt a chill come over him. There was no need for Khonsu to elaborate--he remembered this story from long ago. It had happened under a Greek sun, yes, but the light of the sun has no limits or borders. [line break][line break]";
	wait for any key;
    	say "A case where innocents were slaughtered and the guilty were left unpunished. [line break][line break]";
	wait for any key;
    	say "The story of the Colchian witch Medea. [line break][line break]";
	wait for any key;
    	say "[line break]'Khonsu,' Amun interjected. 'You speak of Medea. Why her?'[line break]";
	wait for any key;
	say "At the sound of 'Medea,' the room fell into a hush. [line break][line break]";
	wait for any key;
    	say "'I had known of her before her crimes had reached the corners of the world,'  Khonsu said. 'Before she had slaughtered her children and tricked the Corinthian princess. Her story was simpler then. but striking in its resemblance to the issue that plagues us today.[line break][line break]";
	wait for any key;
    	say "To further her own goals, Medea had cut her own brother into many pieces and scattered him into the sea.'[line break][line break]";
	wait for any key;
    	say "'I believe,' Khonsu said, 'that when all else has failed us, we must attempt to think like our adversary.'[line break][line break]";
	wait for any key;
	say "Ma’at’s eyes were bright with a newfound hope. 'How,' she said slowly, 'can we expect to counter Seth when none of us possess his cruel cunning? Who else but a Seth to defeat Seth?'[line break][line break]";
	wait for any key;
	say "Amun was at once both horrified and intrigued. 'To invite such a barbaric woman into our home. What have we come to?' [line break][line break]";
	wait for any key;
    	say "But it was their best shot. 'I know a certain Greek sun god. I will ask what can be done.' [line break][line break][line break]";
	wait for any key;
	say "[line break][line break]Your name is Medea. [line break][line break]";
	wait for any key;
    	say "You did not know, despite your contingency plans, where you would go after your triumphant flight from Corinth. [line break][line break]";
	wait for any key;
	say "You really weren’t expecting to end up in a holding cell. [line break][line break]";
	wait for any key;
	say "Your case has been tied up in divine court for a long, long time.[line break][line break]";
	wait for any key;
	say "Hadn’t the gods supported your actions? Why have they punished you in this way?[line break][line break]";
	wait for any key;
	say "And what of Jason? He who had flagrantly broken the most sacred of all oaths. What about his divine punishment? [line break][line break]";
	wait for any key;
	say "[line break][line break]…Oh, right. You already took care of that. [line break][line break]";
	wait for any key;
	say "[line break][line break]One day, Helios comes to your cell. On his face, you see a mixture of pity, aversion, and a strange respect. [line break][line break]";
	wait for any key;
	say "'My dear granddaughter. For many years, my allies and I have argued on your behalf, to little success. [line break][line break]";
	wait for any key;
	say "I have received word from the Egyptian Sun. Egypt has been plagued by an evil it is thoroughly unequipped to combat. Its gods seem to believe that you alone can assist them. [line break][line break]";
	wait for any key;
	say "If you successfully restore order to Egypt, you will be pardoned.'[line break][line break]";
	wait for any key;
	say "'Will I be free?' you ask.[line break][line break]";
	wait for any key;
	say "If so, the answer is clear. [line break][line break]";
	wait for any key;
	say "'What do I need to do?'[line break][line break]";
	wait for any key;
	say "[line break]Your name is Medea, and you can't wait to be free.[line break][line break]";
	wait for any key;
	say "In order to get out of jail, you must gather and reassemble Osiris` body parts. You don't know how many there are or where they might be hidden. [line break][line break]";
	wait for any key;
	say "...You have to hand it to this Seth character. You weren't nearly as creative. [line break][line break]";
	wait for any key;
	say "The Egyptian gods, despite pleading for your assistance, seem to be frightened by you. They've placed some restrictions on your behavior--a parole, if you will. Currently, you cannot access your innate magic and divine power. You will need to rely on your genius, cunning, and astonishingly gorgeous face to complete your mission. [line break][line break]";
	[wait for any key;]
	[say "Your grandfather seems to feel a modicum of sympathy for you. Before he dropped you off in Egypt, he left you with a help book [line break][line break]"; [might re-add if the initial help prompt is not clear for readers]]
	wait for any key;
	say "Your sun chariot has been returned to you. You missed your dragons. After catching up with your darlings, you traveled to Egypt. [line break][line break]";
	wait for any key;
	say "[line break]You are currently in Abydos, which, according to Helios, is home to the cult of Osiris. There's no better place to start. [line break][line break]";
	now the right hand status line is "[map region of the location]";
	pause the game.	

Part 2 - General Game Declarations

Section 1 - Settings

The left hand Vorple status line is "[the player's surroundings]".
The right hand Vorple status line is "[map region of the location]".

When play begins:
	construct the Vorple status line with 2 columns;
	choose row 1 in Table of Basic Help Options; 	
	now description entry is "Even the most clever sorceresses can use a bit of HELP. Fear not--you are in good company! [line break] Select the 'Instructions for Play' to learn more about how to navigate Egypt.[paragraph break]Some general guidelines for this game:[line break]	Make sure to examinine everything![line break]	If you're stuck, it may be time to travel to a new place.[line break]	Medea is very clever, so make sure to pay attention to how she describes things!";

The carrying capacity of the player is 7.

The player is in the Entrance to the Osireion. [starting location]

[This prevents the player from using the TAKE ALL command]
Rule for deciding whether all includes a thing:
	it does not.

[This changes the default message “There are none at all available!”]
Rule for printing a parser error when the latest parser error is the nothing to do error:
	say “Now, now, don’t be greedy.”
	
[for exit lister, reveals the name of rooms when listing exits]
A room memory rule:
	rule succeeds.

Screen-clearing is an action applying to nothing. Understand "clear-the-screen-exec-command" as screen-clearing.

Carry out screen-clearing:
	clear the screen;
	queue a parser command "look", without showing the command.
	
After reading a command:
     remove stray punctuation.
	
Rule for printing room description details of a door:
	stop.
	

Section 2 - New Definitions

To say tab:
	say "    "; [6 spaces]

Talking to is an action applying to one visible thing. Understand "talk to [someone]" or "talk to [something]" or “converse with [someone]” or "converse with [something]" as talking to.

Carry out talking to:
	say "There is no reply."
	
A Body Part is a kind of thing.

Instead of taking a body part:
	now the noun is acquired;
	continue the action.

Instead of dropping a body part:
	say "You'll need that.[line break][line break]Or, well, Osiris will."

A thing can be acquired or unacquired. A thing is usually unacquired.

A thing can be examined or unexamined. A thing is usually unexamined.

Instead of examining something:
	now the noun is examined;
	continue the action.
	
A thing has some text called print-name.

A hint-object is a kind of object. It has some text called hint-content.

To say print-hint (H - a hint-object):
	say "[hint style][bold type](Beginner's Guide)[roman type] [hint-content of H] [end style]".			

To say set-link (target - a thing):
	let link name be "x[target]";
	disable all links called link name; [don't let the player click on old links]
	place a link to the command "examine [print-name of target]" called link name reading "[print-name of target]".
	
Rule for printing the name of a direction (called way) while listing exits:
	if Vorple is supported:
		place a link to the command "go [printed name of way]" reading "[printed name of way]";
	otherwise:
		say "[printed name of way]".

Rule for printing the name of a thing (called obj) when printing the locale description of a room:
	place a link to the command "examine [printed name of obj]" reading "[printed name of obj]".

Section 3 - Sun Chariot Travel

Figure of chariot is the file "chariot.png".

Selecting is an action applying to one topic.
Understand "[text]" as selecting when the player is on the sun chariot.

Carry out selecting:
	let T be the topic understood;
	let input-text be indexed text;
	now input-text is input-text in title case;
	let match found be false;
	repeat through the Table of Transport Options:
		let region-name be "[transport entry]";
		let number-text be "[n entry]";
		if T exactly matches the text region-name, case insensitively or T exactly matches the text number-text, case insensitively:
			let destination be the home entry;
			goto destination;
			now match found is true;
			break;
	if match found is false:
		say "[T] is not a valid destination.";
		list the transport options instead.

To goto (destination - a room):
	move the sun chariot to the destination;
	clear the screen;
	move the player to the destination;	
	if Vorple is supported:
		prefill the command line with "";
		execute JavaScript command "vorple.prompt.unhide()";

To list the transport options:	
	let N be 1;
	say "From here you could choose to go to: [line break]";
	repeat through the Table of Transport Options:		
		say "[tab][N]: ";
		if Vorple is supported:	
			place a link to command "[transport entry]" reading "[transport entry]";
		otherwise:
			say "[transport entry]";
		say "[line break]";
		increment N;
	if Vorple is supported:
		place a link to command "get off chariot" reading "[line break]   (X - Get off chariot)[line break]";
		execute JavaScript command "vorple.prompt.hide()".

After getting off the chariot when Vorple is supported:
	execute JavaScript command "vorple.prompt.unhide()".
	
Report getting off the chariot:
	say "You get off the sun chariot. [line break]" instead.

Understand "ride [the chariot]" as entering.

Table of Transport Options
transport (a region)	home (a room)	n (a number)
Avaris	Avaris-Marketplace	1
Heliopolis	Heliopolis-Marketplace	2
Memphis	Memphis-Marketplace	3
Oxyrhynchos	Oxyrhynchos-Marketplace	4
Amarna	Outside the Walled Village	5
Abydos	Entrance to the Osireion	6
Thebes	Temple of Tawosret 	7
Elephantine	Elephantine-Marketplace	8
[Busiris	Busiris-Marketplace]

Avaris, Busiris, Heliopolis, Memphis, Oxyrhynchos, Amarna, Abydos, Thebes, and Elephantine are regions. 

The sun chariot is an enterable supporter. It is scenery. The sun chariot is in the Entrance to the Osireion. The description of the sun chariot is "Your [sun chariot] is parked, fiercely guarded by your pet dragons."

["the location" refers to the current location of the player]
Every turn:
	if there is a home of the location in the Table of Transport Options and the sun chariot is not in the location:
		move the sun chariot to the location.

After entering the sun chariot:
	say "As you step onto the chariot, your darling dragons hiss joyfully, happy to see you again. [run paragraph on]";
	list the transport options.
	
Rule for printing the name of the sun chariot when the sun chariot is in the location:
	if Vorple is supported:
		place a link to command "get on [printed name of sun chariot]" reading "[printed name of sun chariot]";
	otherwise: 
		say "[printed name of sun chariot]".

[temp room declarations]
Avaris-Marketplace is a room in Avaris. "You have reached a part of the game that is still under development. Please check back soon! :)  [if the sun chariot is in the location]Your [sun chariot] is parked nearby.[end if]"

Heliopolis-Marketplace is a room in Heliopolis. "You have reached a part of the game that is still under development. Please check back soon! :)  [if the sun chariot is in the location]Your [sun chariot] is parked nearby.[end if]"

Memphis-Marketplace is a room in Memphis. "You have reached a part of the game that is still under development. Please check back soon! :)  [if the sun chariot is in the location]Your [sun chariot] is parked nearby.[end if]"

Elephantine-Marketplace is a room in Elephantine. "You have reached a part of the game that is still under development. Please check back soon! :)  [if the sun chariot is in the location]Your [sun chariot] is parked nearby.[end if]"
	
	
Section 4 - Transcript printing
	
[ Copied from: https://intfiction.org/t/extracting-vorple-transcript-from-indexeddb/60125/5]

transcript-on is a truth state that varies. transcript-on is false.

Carry out switching the story transcript on:
	execute JavaScript command "toggleTranscriptButton(true);";
	now transcript-on is true.

Carry out switching the story transcript off:
	execute JavaScript command "toggleTranscriptButton(false);";
	now transcript-on is false.

command-echo is a Vorple style.

After reading a command:
	if transcript-on is true and Vorple is supported:
		let cmd be the player's command;
		say "[command-echo style]>[cmd][line break][end style]";
	continue the action.

The end transcript rule is listed in the shutdown rules.
	
This is the end transcript rule:
	if transcript-on is true:
		try switching the story transcript off.


Part 3 - Intro with Osiris' Head

Head Acquisition is a scene. Head Acquisition begins when play begins. Head Acquisition ends when Osiris' head is acquired. 

Chapter 1 - Abydos-Head
[must be first body piece discovered, no going elsewhere]

Instead of entering the chariot during Head Acquisition:
	say "You've been sent to Abydos for a reason. Maybe try exploring here first."

Section 1 - Definitions

Table of Osiris' unrelated knowledge
comment
"Boop."
"Boop-boop."

Table of Osiris' useful knowledge
fact	summary
1	"Go to Oxyrhynchos! I have fair confidence that my.... a very important.... an important member of my body will be there. "
2	"Fishing may result in some happy results."
2	"If you ever run into a god...you can never go wrong with flattery."

Check talking to head:
	if the head is dumb:
		say "'Osiris,' you demanded expectantly. [paragraph break]";
		choose random row in the Table of Osiris' unrelated knowledge;
		say "'[comment entry]' was the only response you got.";
	otherwise:
		do nothing.

A wall is a kind of thing. A wall is scenery. 

Instead of taking a scenery wall, say "You're not the British Museum. You can't do that."



Section 2 - Entering the Osireion
	
The Entrance to the Osireion is a room in Abydos. "You stand before an inconspicuous side door that supposedly leads to the Osireion. While the workmanship on the [set-link the stone door] is immaculate, it doesn't hold a candle to the monumental entrance for the main temple of Seti I. The area nearby seems empty, with all the traffic instead going toward the main gates. Hopefully, no one will stop you from breaking in. [if the sun chariot is in the location]Your [sun chariot] is parked nearby.[end if]
	[if the stone door is locked][paragraph break]The door to the Osireion is firmly shut. Inscriptions run up and down the doors, but you see no handle. [end if]
	[if Osiris' head is unacquired][paragraph break][print-hint the H1]"
	
H1 is a hint-object. The hint-content is "You can try to GO south through the door or ENTER it. Or perhaps you can take a closer look and EXAMINE the door?".
	
A room memory rule for the North Passage:
	if the north passage is not visited:
		rule fails.

The stone door is a locked closed door. It is scenery. The stone door is south of the Entrance to the Osireion and north of the North Passage. Include (- has animate -) when defining the door. The print-name is "stone door".
The description of the stone door is "The forbidding stone door is engraved with a riddle: [line break]    I am yesterday, veiled in shadows. [line break]    I am tomorrow, cloaked in flames. [line break]    A union of gods, a cycle complete.[if Osiris' head is unacquired][paragraph break][print-hint the H2]"

H2 is a hint-object. The hint-content is "You can SAY your answer TO the door.".

Instead of opening the locked stone door:
	say "You pry at [the noun] with your fingers. It doesn't budge.";
	
Understand "unlock [the stone door]" as a mistake ("A little hard to unlock a door without a lock.").

Instead of answering the door that something:
	if the player's command includes "sun":
		now the stone door is unlocked;
		now the stone door is open;
		now the stone door is unopenable;
		say "As you speak the word 'sun,' the massive stone door grinds open, revealing a long narrow passage directly south of you.";
	otherwise:
		say "Nothing happens. Perhaps that is not the correct answer. You look up at the sky, hoping for some benevolent god to send you a hint.";

The North Passage is a room in Abydos. It is south of the stone door. "The long narrow passage slopes gently downwards. The northern section of the passage is arched and lined with brick. The southern section is stone clad, and its [set-link walls of the north passage] are sculpted and painted with scenes from the 'Book of the Gates'. A pitched roof tops this part of the passage. 
[paragraph break]On the floor, you can see a few miscellaneous items scattered on the floor:[if the ostracon is in the location] a [set-link a hieratic ostracon] ,[end if] a [set-link a tall pottery stand], [set-link trial pieces], and a few [set-link plaster casts].
[paragraph break][print-hint the H3]"

H3 is a hint-object. The hint-content is "Always EXAMINE everything around you! You can try to EXAMINE the walls and the things on the floor. You can also TAKE things. When you are disorientated, feel free to take another LOOK at the room.".

Rule for listing nondescript items of the North Passage:
	do nothing.

A hieratic ostracon is a thing in the North Passage. The description of the ostracon is "A shard of pottery is stained with hastily written hieratic, seemingly from when the complex was under construction. It reads: 'Fourth month of prt Day 22, Amount of work done by the Gang of the Left. What it dragged from the Quay of the Fortressof Menmaatre to the south of Menmaatre is Beneficial to Osiris...'". The print-name is "hieratic ostracon"

The walls of the north passage is a wall in the North Passage. The description is "A representation of the fore Osiris and Nut, who are merely subordinate sunrise, is painted on the East wall. Sculpted on the West wall is the sunset." The print-name is "walls".

Understand "wall" as the walls of the north passage.

A tall pottery stand is a thing in the North Passage. The description is "An unassuming pottery stand, its form typical of the 19th dynasty." The print-name is "tall pottery stand".

Instead of taking the tall pottery stand, say "It's awfully burdening to carry around for as little use as it is to you."

Trial pieces of a sculptor is a thing in the North Passage. The description is "Two sculpted trial pieces probably from the same school. The first piece is an attempt at the neb sign. Above is the master's hand as an example, and below are the student's attempts 'in every degree of badness'. The other piece shows a scene of the worship of Osiris. It is unfinished, only one figure having been sculpted, the rest being merely sketched out in black ink." The print-name is "trial pieces of a sculptor".

Instead of taking trial pieces, say "You distastefully look at the amateur scribbles. Bleh."
	
Plaster casts are a thing in the North Passage. The description is "Casts of eyes of statues and of details of decorations, probably from the temple of Ramesses." The print-name is "plaster casts".

Instead of taking plaster casts, say "Ooh, fun little figures! If only you cared." 

The Great Hall is a room in Abydos. It is south of the North Passage. "The Great Hall widens from the Northern Passage into a long rectangular room. The floor, similar to that of the South Chamber and the passages, is paved with sandstone, and the [set-link walls of the great hall] are covered with inscriptions. The roofing stones stretched the whole width of the hall, without any pillars or other support. "

The walls of the great hall is a wall in the Great Hall. "The North and South Walls are covered with inscriptions. The West Wall is divided into three parts vertically; the portion to the left hand is filled with a colossal scene of the Vivification of Osiris; the middle portion contains part of the Chapter of knowing the Names of Osiris; the right hand portion is occupied with the figure of King Merenptah standing before a heaped-up table of offerings, and making an offering of incense. The wall has a frieze of the kheker ornament painted in yellow, blue, green, and black." The print-name is "walls".
	
Understand "wall" as the walls of the great hall.

The South Chamber is a room in Abydos. It is south of the great hall. "You are now in the south chamber. [if Osiris' head is unacquired][paragraph break][print-hint the H4]"

H4 is a hint-object. The hint-content is "Whenever faced with random objects, try to EXAMINE or TAKE them. There may be some potentially useful items in the mishmash...".

[x wall]
[small squatting statuette]

The walls of the south chamber is a wall in the South Chamber. The description is "Parts of the Book of the Dead is inscribed across these walls."

The faience ankh is in the South Chamber. The description of the faience ankh is "A delicate yet sturdy ankh, the symbol of life, carved from faience. " 

Some pottery jars are in the South Chamber. The description of the pottery jars is " Several small jars of varying shapes and sizes rest against the wall. Their surfaces are worn. Traces of paint suggesting they once bore vibrant designs. Among them, you see a broken jar with unguents and another fallen to the floor, with dried red stains around the rim."

A wooden plank is in the South Chamber. The description of the wooden plank is "A short, splintered plank of cedar wood."

Some knick-knacks are in the South Chamber. The description of some knick-knacks is "You sift through the assorted items: a bead that may have been part of a necklace, a sliver of ivory that was used as a tool, and a draining liquid water clock."

The Sloped Passage is a room in Abydos. It is east of the Great Hall.

[x wall]

The First Transverse Chamber is a room in Abydos. It is east of the Sloped Passage. "To your east you see the central hall, grand and collonaded. However, surrounding the entire central hall island is a [set-link moat], preventing you from going through. If only you had something that help you cross the water..." 
	
[x wall]

A moat is a backdrop. It is in the First Transverse Chamber, the Central Nave, and the Tomb of Osiris. The description is "Deep and murky." The print-name is "moat".

[somehow add info about the weird moat stuff, also mention Strabo]

Instead of going from the Central Nave:
	if the player does not have the wooden plank:
    		say "You approach the edge of the moat, but there is no way across. The waters churn ominously, as if warning you to stay back.";
	otherwise: 
    		say "You place the wood plank down, desperately hoping it'll bear your weight.";
		wait for any key;
		continue the action;
		
Instead of going to the Central Nave:
	if the player does not have the wooden plank:
    		say "You approach the edge of the moat, but there is no way across. The waters churn ominously, as if warning you to stay back.";
	otherwise: 
    		say "You place the wood plank down, desperately hoping it'll bear your weight.";
		wait for any key;
		continue the action;
		
The Central Nave is a room in Abydos. It is east of the First Transverse Chamber. "You stand among the columns of the central nave, turning slowly to take in your surroundings. A [moat] surrounds the entire central nave, like primeval waters around the first land. Seventeen small cells line the walls beyond the moat, most of which are shrouded in darkness. One directly east opens toward a bigger chamber. [paragraph break]The ceiling echoes the structure of a sarcophagus. A large [set-link the statue of Osiris] dominates the center of the room, its scale representative of what could have only been a god. A [funerary bier] sits in front of it, shaped like two side by side lions. "
	
The cells are scenery in the Central Nave. The description is "Of the seventeen cells, sixteen are covered by rotting wood doors."
	
[x cells - wooden doors closed until ritual time at reunification] 
[x stairs - leading into the water]
[x pillars, x ceiling]

The statue of Osiris is in the Central Nave. It is scenery. The description of the statue of Osiris is "A large statue of Osiris ias seated at the top of a staircase. The enthroned Osiris faces towards the sarcophagus chamber, just as Osiris faces inward with his back to the entrance of the Osireion in the Book of Gates. He holds a crook and the ankh sign, signifying that he rules over his domain, the duat, and is the possessor of life. He sure looks put together -- such a shame he's all chopped up now." The print-name is "statue".

The funerary bier is a supporter in the Central Nave. The description of the bier is "A bier used for embalming practices, fashioned to resemble Sekhmet, the grumpy kitty-cat daughter of Ra, the VEnGeFul EYe."

[two statues, one sitting, and an empty bier on the longer divot (for future osiris pieces to go on)]

Before going when the location is the Central Nave:
	if the noun is east or the noun is west:
		continue the action;
	otherwise:
		say "With the moat surrounding the central nave, it's a struggle to go anywhere." instead.

The Tomb of Osiris is a room in Abydos. It is east of the Central Nave. "The [set-link the painted roof] and [set-link the inscribed walls] has depictions of Nut and Geb. At the center of the room is a [set-link the sarcophagus], its wooden frame lavishly painted in the style of the New Kingdom. [if sarcophagus is locked]You feel that there is something--or someone--important inside..."

[x wall, x ceiling]

The painted roof is scenery in the Tomb of Osiris. The description of the painted roof is "The depiction of Nut, her star-strewn body stretching across the heavens, is both graceful and imposing. Beneath her, Geb lies in repose, his green-toned skin symbolizing fertility and life. The colors have dulled over time, but the craftsmanship is evident." The print-name is "roof".

The inscribed walls are scenery in the Tomb of Osiris. The description of the inscribed walls is "The hieroglyphs here are meticulously carved, detailing invocations to the gods and protective spells. Some portions of the text are damaged, yet the overall message of guidance and protection is clear." The print-name is "walls".

The sarcophagus is a closed, locked container. It is scenery. It is in the Tomb of Osiris. The description of the sarcophagus is "The sarcophagus is a masterpiece of New Kingdom artistry. Crafted from cedar wood, its surface is painted with intricate designs. Bright swirls of blue decorated much of the lid, complete with tiny figures and symbols of life, prosperity, and health. Its occupant must have made it auto-update every now and then to keep up with the most popular trends. [if Osiris' head is unacquired][paragraph break][print-hint the H4a]". The sarcophagus has matching key the faience ankh. The print-name is "sarcophagus".

H4a is a hint-object. The hint-content is "To find out what's inside, you better OPEN it first."

Instead of opening the locked sarcophagus:
	say "The sarcophagus seems stuck, almost as if sealed shut by magic. [if Osiris' head is unacquired][paragraph break][print-hint the H5][line break]".

H5 is a hint-object. The hint-content is "Perhaps you can use something to UNLOCK it."

Instead of unlocking the sarcophagus with the faience ankh:
	now the sarcophagus is unlocked;
	now the sarcophagus is open;
	say "You unlock the sarcophagus. The head of Osiris is inside![paragraph break]";
	wait for any key;
	say "...but the rest of him isn't. What a pain. As much as you are a firm believer of the old chop-and-scatter method, you wish Seth hadn't been so diligent.".

Osiris' head is a body part. It is in the sarcophagus. Osiris' head can be taken. Understand "Osiris" as Osiris' head. Include (- has animate -) when defining Osiris' head. Osiris' head can be smart or dumb. Osiris' head is dumb. The description of Osiris' head is "Osiris, in all his glorious, head-only form. He's green."
		
Test head with "look / unlock sarcophagus with ankh / take head" in Tomb of Osiris holding faience ankh.
		

 Part 4 - Talking Osiris Agenda
[this entire part must be completed in order]

Talking Osiris Agenda is a scene. Talking Osiris Agenda begins when Head Acquisition ends. Talking Osiris Agenda ends when Heart Acquisition ends. 

After taking Osiris' head for the first time:
	say "Aha! The head! You triumphantly hold the head of Osiris. How easy! Even someone like J*son could have solved this riddle! You'll be free in no time. You just need the torso, legs, and arms--then you're done! [paragraph break]";
	wait for any key;
	say "Although his eyes are open, Osiris remains silent.[paragraph break]";
	wait for any key;
	say "You've got a bad feeling about this.[paragraph break]";
	wait for any key;
	say "'Hello?' you ask. [paragraph break]";
	wait for any key;
	say "...No reply.[paragraph break]";
	wait for any key;
	say "You shake the head, hoping to wake it up. There's still silence. And it's remarkably light for its size. Could it be..that there's no brain inside?[paragraph break]";
	wait for any key;
	say "...[paragraph break]";
	wait for any key;
	say "It seems this will be more difficult than you anticipated.[paragraph break]";
	wait for any key;
	say "The head, in its stubborn silence, seems to taunt you.[paragraph break]";
	wait for any key;
	say "...Wait a moment![paragraph break]";
	wait for any key;
	say "There's a little papyrus scrap tangled in the beard of Osiris. You pull it out and unfold it, reading the following:[paragraph break]	The sun of the shattered city awaits you,[line break]	where you can find the largest brains in the largest skulls.[paragraph break]";
	wait for any key;
	say "Hmm. What could that mean?[paragraph break]";
	wait for any key;

Chapter 2 - Amarna-Brain

Brain Acquisition is a scene. Brain Acquisition begins when Talking Osiris Agenda begins. Brain Acquisition ends when Osiris' brain is acquired.

Discovery is a scene. Discovery begins when Brain Acquisition begins. Discovery ends when the workman is curious. 

Get Body Bag is a scene. Get Body Bag begins when Discovery ends. Get Body Bag ends when the body bag is acquired.

Exploration is a scene. Exploration begins when Get Body Bag ends. Exploration ends when Osiris' brain is acquired.

Next Instructions is a scene. Next Instructions begin when Exploration Ends. Next Instructions end when the workman is uncurious.
	
Section 2 - Workmen's village

Outside the Walled Village is a room in Amarna. "You have reached Amarna, although you don't really know where to go from here. You are surrounded by quite a bit of rubble. This seems to have once been a great city to the west. Directly north, you see a walled cluster of buildings and hear the faint sound of hubbub. This might be a workmen's village. But it is entirely walled and only has one gate. That seems to be a fire hazard. You would know about those.  [if the sun chariot is in the location]Your [sun chariot] is parked nearby.[end if]";

[in the future, maybe add security checkpoint moment as Medea walks through gate?]

The Workmen's Village is a room in Amarna. It is north of Outside the Walled Village. "A dusty cluster of homes and workshops, alive with the clatter of tools and the murmur of voices. The sun shines down aggressively, catching the haze of dust kicked up by passing feet. People move between doorways and shaded courtyards—some carrying baskets of stone chips, others bent over their work."

A body bag is a container. The carrying capacity of the body bag is 14. The description of the body bag is "A heavy cloth sack used for transporting... questionable things. You're not quite sure how everything fits into here. Some strange Egyptian magic?"

Check inserting something into the body bag:
	if the noun is not a body part:
		say "The body bag isn't for [the noun]. If you put something unrelated, it would just be a bag!" instead.

A workman is a person. The workman carries a body bag. The workman can be curious or uncurious. The workman is uncurious. The description of the workman is "A middle aged man, probably with degenerative joint disease."

Rule for writing a paragraph about the workman when the location is the Workmen's Village:
	say "Your scan the crowd and see a [workman], staring irritably at the distance.[first time][line break][print-hint H5a][line break][only]".

H5a is a hint-object. The hint-content is "Maybe you could TALK to someone.".

When Brain Acquisition begins:
	now the workman is in the Workmen's Village.

[maybe in future make player have to "hide head" first]
Instead of talking to the workman during Discovery:
	say "You suddenly realize that you are holding a decapitated head in broad daylight. You hastily cram the head under your shirt and try not to look suspicious. Now you're ready to make human conversation. [paragraph break]";
	say "'Hey, you.' you called to the workman.  [paragraph break]";
	wait for any key;
	say "The workman turns around with a disgruntled expression. 'What do you want, lady?' he asks.  [paragraph break]";
	wait for any key;
	say "You explain that you're looking for information about the village, conveniently omitting why exactly you are interested.  [paragraph break]";
	wait for any key;
	say "The workman gestures around. 'This village,' he says, 'it's where the elite tombs nearby are prepared. Hard work, grueling work, but someone's got to do it. Not that they treat us well for it.' He looks a little more closely at you. 'What are you doing here, anyway? And good gods, are you pregnant? Triplets?!'";
	now the workman is curious.
	
Instead of talking to the workman during Get Body Bag:
	say "The workman looks at you expectantly. 'What are you doing here, anyway? And what's up with your stomach?'"

Instead of talking to the workman at least three times during Get Body Bag:
	say "He seems to be expecting an answer from you. Maybe try showing him what's hidden under your shirt. [paragraph break] The scary part, not the sexy part. [paragraph break][print-hint the H6]"
	
H6 is a hint-object. The hint-content is "Take note of key words hidden in the text. They might just SHOW you what to do...".
	
Instead of showing the head to the uncurious workman during Discovery:
	say "Flashing a decapitated head to someone you've never talked to before could be overkill. Try striking a conversation first."
		
Instead of showing the head to the curious workman during Get Body Bag:
	say "You carefully take the head from under your shirt and show it to the workman. 'Do you have anything to go along with this?' you ask.[paragraph break]";
	wait for any key;
	say "The workman eyes you warily. 'You collect heads, huh? I knew a guy like that once. There's a reason we put up walls around here, you know... If you want heads, that old Aten cult temple's the place to check. Or what remains of it, at least.'  [paragraph break]"; 
	wait for any key;
	say "You give a nod of thanks and turn to leave. Before you can go, he calls to you. 'Hey! I don't know what your whole deal is, but here. It's good manners to keep body parts inside a body bag--they start to stink when left out under the sun. Trust me, I would know.' [paragraph break] He tosses you a body bag. It smells strange, but you've dealt with--and dealt--worse.";
	now the workman is uncurious;
	now the player is holding the body bag;
	now the body bag is acquired.

Instead of talking to the workman during Exploration:
	say "The workman looks at you with exasperation. 'Lady, can't you see I've got a job to do? I've told you everything I know. Go to that old Aten temple for your weird heads."
	
Instead of showing the head to the uncurious workman during Exploration:
	say "You don't think showing off Osiris` head again is necessary. Even if it might be funny.".
	
Section 3 - Traveling to the temple

An oxcart is a rideable vehicle. "An [oxcart] sits here unattended." The description of the oxcart is "A sturdy wooden cart, drawn by a strong hunky ox."

When Exploration begins:
	now the oxcart is in Outside the Walled Village.

The Ruined City is a room in Amarna. It is west of Outside the Walled Village. "This glorious city, once the capital of Egypt, is nothing but ruins and foundations. In the distance, to the north, you see a small standing structure. It sticks out like a sore thumb amongst the desolation."

Instead of going to the Ruined City when the player is not on the oxcart:
	if player is in Outside the Walled Village:
		say "It's a long journey to Amarna City: you'll never make it on foot.";
	otherwise:
		continue the action.

Instead of going to Outside the Walled Village when the player is not on the oxcart:
	if player is in the Ruined City:
		say "It's a long journey to the village: you'll never make it on foot.";
	otherwise:
		continue the action.

After going to the Ruined City:
	if the player is in Outside: 
		say "You board the oxcart and start traveling to the temple.";
	continue the action.
	
After going to Outside the Walled Village:
	if the player is in the Ruined City:
		say "You board the oxcart and start traveling to the temple.";
	continue the action.

Instead of going to the Small Aten Temple when the player is on the oxcart:
	say "You can hardly ride an oxcart into the temple (even a demolished one)! Better not piss off the Egyptian gods as well."
	
Instead of going to the Workmen's Village when the player is on the oxcart:
	say "The security at the entrance stops you. No animal-drawn vehicles in the village!"

The Small Aten Temple is a room in Amarna. It is north of the Ruined City. "The traces of this Aten Temple still hint at its former grandeur. It once comprised three courts—the first, the second, and the sanctuary. Now you stand in what used to be the sanctuary court, its original form long since demolished. A partially reconstructed sanctuary is to your north. It is still impressive despite representing only a fraction of its former splendor."

The Sanctuary is a room in Amarna. It is inside of the Small Aten Temple. "Unlike sanctuaries in traditional temples, this space is bathed in sunlight, a deliberate design to honor Aten, the sun disk. Around the perimeter of the sanctuary are low, carved [set-link carved altars], decorated with [set-link offerings] and incense. In the center of the sanctuary, a [set-link shrine] depicting Aten shines in its golden splendor."

The carved altars are scenery in the Sanctuary. "The low altars are made of sandstone, each inscribed with dedications to Aten." The print-name is "altars".

The offerings are things in the Sanctuary. "Arranged delicately atop the altars are offerings of bread and beer. They looks fresh, as if newly placed by worshippers. Odd. Wasn't this place abandoned?". The print-name is "offerings".

The Aten shrine is scenery in the Sanctuary. "At the heart of the sanctuary stands a majestic shrine dedicated to Aten. The shrine captures and reflects the abundant sunlight that bathes the room. The central depiction of the Aten radiates golden beams that dance across the walls, filling the space with a divine glow." The print-name is "shrine".

The hidden staircase is a secret door. The staircase is above the Underground chapel and below the Sanctuary. 

Instead of examining the shrine:
	now the hidden staircase is revealed;
	now the hidden staircase is open;	
	say "[the description of the shrine][paragraph break]";
	say "You hear an odd rumbling noise. There was a hidden staircase! You can go down to see what's there.[paragraph break]".

Underground Chapel is a room in Amarna. It is below the staircase. "The air is cool and still. There are three strange statues ([set-link first statue], [set-link second statue], [set-link third statue]) and murals ([set-link north mural], [set-link east mural], [set-link south mural], [set-link west mural]) with eerily etched toes line all four walls. The [set-link ceiling] depicts an intricate scene. [if unvisited][paragraph break]What odd surroundings. Perhaps you should take a closer look. The chapel is completely empty, so there's also no harm in touching something--in fact, that might help you figure things out."


Section 4 - At the Small Aten Temple
	
The north mural is scenery in the Underground Chapel. The description is "A radiant sun dominates the mural, its rays reaching outward. Each ray ends in delicate, finger-like extensions, as though grasping for the world below. It's as if the sun itself were alive, bestowing its blessings on all it touches." The print-name is "north mural".

The south mural is scenery in the Underground Chapel. The description is "This mural depicts a gathering of figures, each etched with exquisite detail. Curiously, the figures here have extremely defined toes...you're not quite sure why." The print-name is "south mural".

The east mural is scenery in the Underground Chapel. The description is "The weathered relief on the eastern wall shows nothing but a simple, unadorned sun disk. Time has erased the surrounding details, leaving only faint traces of the elaborate etchings." The print-name is "east mural".

The west mural is scenery in the Underground Chapel. The description is "This mural shows a man makes an offering to Aten. Cradled carefully in his hands is...a small duck?[paragraph break]Ha. You would never settle for such meager offerings." The print-name is "west mural".

Understand "north wall" or "n wall" or "north mural" as the north mural when the location is the Underground Chapel.

Understand "south wall" or "s wall" or "south mural" as the south mural when the location is the Underground Chapel.

Understand "east wall" or "e wall" or "east mural" as the east mural when the location is the Underground Chapel.

Understand "west wall" or "w wall" or "west mural" as the west mural when the location is the Underground Chapel.

The ceiling is scenery in the Underground Chapel. The description is "You crane your head up to look at the ceiling. There seems to be a lot going on here. You can see 1...2...3...4 major components. A sun disk is being held in the hands of a praying man. Across from him, a man with a very long head is smelling a lotus." The print-name is "ceiling".

The first statue is scenery in the Underground Chapel. The description is "A broken statue that probably once depicted a person. Guess we'll never find out. You feel a strange kinship with this statue." The print-name is "first statue".

The second statue is scenery in the Underground Chapel. The description is "A statue of a man with a ridiculously long head. He looks remarkably like a man you once knew." The print-name is "second statue".

The third statue is scenery in the Underground Chapel. The description is "A statue of a man with a giant lotus sprouting out the top of his head. That looks painful." The print-name is "third statue".

After examining something in the Underground Chapel during Exploration:
	if the noun is scenery:
		if the noun is examined:
			say "[The noun] reveals nothing new to you. Maybe it's time to try touching things?";
		now the noun is examined.


Instead of touching something in the Underground Chapel:
	if there is no item in row 1 of the Table of Current Sequence:
		continue the action;
	choose row 1 in the Table of Current Sequence;
	if the noun is the item entry:
		blank out the whole row;
		sort the sequence;
		say "You touch the [noun], and something clicks into place.";
		if there is no item in row 1 of the Table of Current Sequence:
			trigger the end sequence;
	otherwise:
		say "Nothing happens.";
		refill the sequence.

Table of Full Sequence
seq	item
1	east mural
2	west mural
3	second statue
4	third statue

Table of Current Sequence
seq	item
1	east mural
2	west mural
3	second statue
4	third statue
with 1 blank row

To sort the sequence:
	sort the Table of Current Sequence in seq order;

To refill the sequence:
	blank out the whole of the Table of Current Sequence;
	repeat with N running from 1 to the number of rows in the Table of Full Sequence:
		choose row N in the Table of Full Sequence;
		let seq-val be the seq entry;
		let item-val be the item entry;
		choose row N in the Table of Current Sequence;
		now the seq entry is seq-val;
		now the item entry is item-val.

To trigger the end sequence:
	say "You hear something from up above! Looking up at the ceiling, you see a gray [set-link the brain] fall from the duck-bearing man's head right to your feet. Is that a...?";
	move Osiris' brain to the Underground Chapel.
	
Test brain1 with "talk to workman / show head to workman / gonear chapel" in the Workmen's Village.

Test brain2 with "touch east mural / touch west mural / touch second statue / touch third statue / take brain" in Underground Chapel.

Osiris' brain is a body part. Understand "brain" as Osiris' brain. The description of the brain is "A brain. It feels like it is pulsating in your hands." The print-name is "[if unexamined]lump[otherwise]brain[end if]".

Understand "lump" as Osiris' brain.

After examining the brain:
	now the brain is examined.

Instead of taking the brain when the player's command includes "lump":
	if Osiris' brain is unexamined:
		say "Better not take something you don't know.";
	otherwise:
		continue the action.

After taking Osiris' brain:
	say "With great anticipation, you take the brain and plop it right into the head.[paragraph break]You patiently wait for Osiris to regain intelligent thought and offer you his utmost thanks.[paragraph break]";
	wait for any key;
	say "...[paragraph break]";
	wait for any key;
	say "...[paragraph break]";
	wait for any key;
	say "...[paragraph break]";
	wait for any key;
	say "Any moment now.[paragraph break]";
	wait for any key;
	say "...[paragraph break]";
	wait for any key;
	say "...[paragraph break]";
	wait for any key;
	say "Nothing happened.[paragraph break]";
	wait for any key;
	say "Is he just stupid?!?[paragraph break]";
	wait for any key;
	say "Well, at least you have one more piece of the puzzle. But you might need to go speak with someone else in Amarna if you want any actual clues.";
	wait for any key;

Section 5 - Back to the village

When Exploration ends:
	now the workman is in Outside the Walled Village;
	now the workman is curious.

Instead of entering the sun chariot when the player is on the oxcart:
	say "You have to get off of one carriage to get on another."
	
[so medea goes back to the village on oxcart, immediately she is greeted by the workman (aka dialogue triggered automatically, they start talking]
Every turn during Next Instructions:
	if the location is Outside the Walled village:
		now the workman is in Outside the Walled Village;
		say "As you arrive, you spot into the same workman you had spoken with earlier.[paragraph break]";
		wait for any key;
		say "'Hey, lady!' he says. 'You got those heads of yours now?' He looks a little less wary.[paragraph break]";
		wait for any key;
		say "'Close enough,' you say.[paragraph break]";
		wait for any key;
		say "'It's nice to see a new face, even if it's a creepy one,'  the workman continued. 'This place has been a ghost town for so long. We used to be as big as Thebes, you know!'[paragraph break]";
		say "...Thebes?[paragraph break]";
		wait for any key;
		say "The workman notices your confusion. 'You must really be a foreigner! Don't you know of Thebes? All the tomb workers are there nowadays.'[paragraph break]";
		wait for any key; 
		say "...Interesting.[paragraph break]";
		wait for any key; 
		say "...[paragraph break]";
		wait for any key; 
		say "...[paragraph break]";
		wait for any key; 
		say "One of your potential escape plans -- not that you had the opportunity to go through with it -- was to flee from Corinth to Thebes. But you never had the chance to execute this plan. Perhaps this is a sign.";
		now the workman is uncurious. [next instructions end]


Carry out talking to the uncurious workman:
	say "The workman grunts, uninterested in further conversation." instead.
	
	
test Amarna with "look / n" in The small aten temple holding the head.

Chapter 3 - Thebes-Heart

Heart Acquisition is a scene. Heart Acquisition begins when Brain Acquisition ends. Heart Acquisition ends when Osiris' heart is acquired.

Heal Heracles is a scene. Heal Heracles begins when Heart Acquisition begins. Heal Heracles ends when Heracles is healthy. 

Buddy Heracles is a scene. Buddy Heracles begins when Heal Heracles ends. Buddy Heracles ends when the location of the player is not in Busiris and the heart scarab is acquired.

Every turn during Buddy Heracles:
	if the location of Heracles is not the location of the player:
		now Heracles is in the location;
		if the location is in Busiris:
			say "Heracles follows you with a grunt.".

Test heal with "purloin prepared medicine / I have indeed applied something to all his sore spots" in the temple of Horemheb.


Section 1 - All the Locations

The Temple of Thutmoses III is a room. It is in Thebes. "This temple honors Thutmoses III. Sturdy columns flank a central courtyard where priests once performed daily rites. There seems to be a lot of honoring going on around here."

The Ramesseum is south of The Temple of Thutmoses III. It is in Thebes. "[set-link mortuary temple] looms large with its colossal statues. [set-link faded reliefs] along the walls depict the famous Battle of Kadesh. An inscription points west to a mysterious 'House of Life,' rumored to store sacred scrolls."

The mortuary temple of Ramesses II is scenery in the Ramesseum. "Ramesses the Great—you had heard of him before, although by a different name. This place had once been a monumental temple suitable for the King of Kings. Now all that remains are sun-bleached ruins." The print-name is "The mortuary temple of Ramesses II".

The faded reliefs is scenery in the Ramesseum. The print-name is "Faded reliefs".

Instead of examining the faded reliefs:
	say "An infamous battle between the Egyptians and Hittites. A thrilling tale of subterfuge, although not one you'd want to repeat. Muwatalli II, the Hittite king, had dispatched two spies who tricked Ramesses II into believing the Hittite army was far off, when in fact it lay in wait. As Ramesses marched on, two more Hittites--this time actual prisoners--confessed that the enemy was just around the corner. The trick had run its course, so the Hittites attacked. Each side commanded thousands of infantrymen and charioteers, and in the end, both sides thought they had won. In other words, it was a stalemate. [paragraph break]";
	wait for any key;
	say "But this... appears to show an entirely different kind of battle. A massive Egyptian figure fends off the Hittites alone, with no other Egyptians in sight. Is that towering figure... Ramesses II? [paragraph break]";
	wait for any key;
	say "Near the bottom of the relief, you notice grafitti scrawled onto the stone. It reads: 'Did he really fight all alone?'"
	
	
Understand "walls" or "reliefs" or "Battle of Kadesh" as faded reliefs when the location is the Ramesseum. 

The Temple of Thutmoses IV is south of the Ramesseum. It is in Thebes. "An elegant temple commemorates the reign of Thutmoses IV. Columns and archways display finely chiseled hieroglyphs extolling the king’s diplomatic prowess."

The Temple of Tawosret is south of the Temple of Thutmoses IV. It is in Thebes. "The Temple of Tawosret stands here, relatively modest compared to its neighbors but still adorned with intricate carvings. You can look around Thebes for something—or maybe someone—useful. [if the sun chariot is in the location]Your [sun chariot] is parked nearby.[end if]"

The Temple of Merenptah is south of the Temple of Tawosret. It is in Thebes. "The Temple of Merenptah is here, partially ruined yet still imposing."

A Collection of Three Temples is a room. It is south of the Temple of Merenptah. It is in Thebes. "You see the temples of Thutmoses I, Amenhotep Son of Hapu, and Thutmoses III."

The Temple of Thutmoses I is scenery in the Collection of Three Temples. "The Temple of Thutmoses I is here, flanked by a row of weathered sphinxes."

The Temple of Amenhotep Son of Hapu is scenery in the Collection of Three Temples. "The Temple of Amenhotep, Son of Hapu is here. He was revered as a scribe and later deified for his great wisdom."

The Smaller Temple of Thutmoses III is scenery in the Collection of Three Temples. "The smaller Temple of Thutmoses III is here. It's also honoring Thutmoses III  -- just at a smaller scale."

The Temple of Amenhotep III is east of the the Collection of Three Temples. It is in Thebes. "Amenhotep III's vast mortuary temple lies here. Even in partial ruin, the temple reflects the grandeur of his reign."

The Memnon Colossi is east of the Temple of Amenhotep III. It is in Thebes. "Perched like silent sentinels, two massive statues of Amenhotep III ([set-link the left statue], [set-link the right statue]) watch over the land. Legend says their eerie dawn 'singing' once greeted travelers."

The left statue of Amenhotep III is a closed unopenable container. It is scenery in Memnon Colossi. The print-name is "left statue".

The right statue of Amenhotep III is a closed unopenable container. It is scenery in Memnon Colossi. The print-name is "right statue".

Instead of cutting the The right statue of Amenhotep III, do nothing.

Instead of cutting the The left statue of Amenhotep III, do nothing.

Osiris' heart is a body part. It is in right statue of Amenhotep III.

The heart scarab is a thing. The description is "The scarab is a small but intricately made item. Inscribed on the bottom is the cartouche of Amenhotep III. [if heart is unacquired] Perhaps it's worth investigating..."

Instead of putting the heart scarab on the right statue of Amenhotep III:
	now right statue of Amenhotep III is open;
	now right statue of Amenhotep III is unopenable;
	say "The panel at the base of the statue slides open to reveal the heart of Osiris. Hooray![paragraph break]".


The Temple of Horemheb is south of the collection of three temples. It is in Thebes. "This temple stands as tribute to Aÿ and Horemheb."

The Temple of Ramesses III is south of the Temple of Horemheb. It is in Thebes. "Here be Ramesses III's temple. You feel that this man met a bitter end. You can respect that."

Section 2 - The library

[depending on feedback, can try to make entering house of life more intuitive: aka somehow medea knows house of life is library and it is inside the ramesseum. maybe a trigger of dialogue like the priest asks medea her purpose and she says for medicine - not priority]

The House of Life is west of the Ramesseum. It is in Thebes. "The entrance is unassuming—just a low archway tucked beside the temple’s western wall—but when you step inside, you realize you've stumbled upon a vast library.  Soft lamplight flickers across rows of tall shelves, each lined with carefully rolled [set-link the medical papyri] detailing magical and medical rituals.  [paragraph break] You scan the shelves, looking for something useful. There's a certain scroll that caught your eye..."

[some mechanic for finding correct papyri or simple description]

Medical papyri is scenery in the House of LIfe. The description of the medical papyri is "A headache spell. It reads: [paragraph break] -YET ANOTHER- CONJURATION FOR THE HEAD [line break]
Horus is fighting with Seth for the Unique Bush -- a hmm-plant which Geb had brought forth. Re, listen to Horus! Should he keep silent (tmn) because of Geb? Horus is suffering from his head! Give him something to dispel his torments, Isis! Take a decision, mother of Horus! [line break] 'I have indeed applied something to all his sore spots.' [line break] Words to be said over buds of a Unique Bush. To be twisted leftwise, to be soaked in mucus, and the bud of a snb-plant laced to it. To be fitted with 7 knots and to be applied to a man's throat.[paragraph break] Once you have these items, you can PREPARE them to make the medicine. You can always check your inventory to see what you have.". The print-name is "papyri".

Understand "scroll / papyri" as the medical papyri.

Instead of examining the papyri:
	now the papyri is examined;
	continue the action.

[after examining papyri, player can type "think" to remember what she needs]
Instead of thinking during Heal Heracles:
	if the papyri is examined:
		say "-YET ANOTHER- CONJURATION FOR THE HEAD [line break] Horus is fighting with Seth for the Unique Bush -- a hmm-plant which Geb had brought forth. Re, listen to Horus! Should he keep silent (tmn) because of Geb? Horus is suffering from his head! Give him something to dispel his torments, Isis! Take a decision, mother of Horus! [line break] 'I have indeed applied something to all his sore spots.' [line break] Words to be said over buds of a Unique Bush. To be twisted leftwise, to be soaked in mucus, and the bud of a snb-plant laced to it. To be fitted with 7 knots and to be applied to a man's throat.[paragraph break] Once you have these items, you can PREPARE them to make the medicine. You can always check your inventory to see what you have.".

Section 3 - Healing Heracles

[when medea first lands, say "maybe it's time to explore to gather some hints about where Osiris might be]

Heracles is a man. Heracles can be sick or healthy. Heracles is sick. The description of Heracles is "A muscled man wearing lion skin and carrying a club. [if Heracles is sick] He is lying face down on the floor, moaning in agony. Perhaps you should help him? Only because he might be useful, of course. But how?"

Instead of talking to sick Heracles for the first time:
	say "'Is that...Heracles?' you called out.[paragraph break]";
	wait for any key;
	say "The only response you received was an agonized groan.[paragraph break]";
	wait for any key;
	say "You get a little closer. He seems... unwell, to say the least.[paragraph break]";
	wait for any key;
	say "You reach out and poke him in the shoulder. 'What's wrong with you?'[paragraph break]";
	wait for any key;
	say "Heracles flops onto his back and looks at you with bleary, half-focused eyes. 'Oh, kind stranger, I am in the most unfathomable pain! A foreign monster has made its home in my skull. Please, help me banish this beast!'[paragraph break]";
	wait for any key;
	say "...A headache. Should you help him?[paragraph break]";
	wait for any key;
	say "...[paragraph break]";
	wait for any key;
	say "...[paragraph break]";
	wait for any key;
	say "You might as well take this community service thing to its fullest."

When brain acquisition ends:
	now Heracles is in the Temple of Horemheb.

The brewing table is a supporter in the House of Life. "A sturdy wooden table covered with various random items, perfect for preparing magical concoctions. Among the clutter, a vial catches your eye."

Some random items are things on the brewing table. The description is "A collection of unrelated trinkets and items: old coins, scraps of paper, and fragments of pottery. None seem particularly useful."

The vial of plant mucus is on the brewing table. The description is "A small glass vial with extracted plant mucus inside."

The buds of a Unique Bush are in the Ruined City. The description is "A rare and magical bush said to have healing properties. It bears buds of a mystical nature." 

Rule for printing the name of the Buds of a Unique Bush when the Buds of a Unique Bush are in the Ruined City:
	say "Unique Bush".

A bud of a snb-plant is in the South Chamber. The description is "The bud of a mysterious plant, tied up with some string." 

The twisted Unique Bush buds are a thing. The prepared Unique Bush buds are nowhere.
The soaked Unique Bush buds are a thing. The soaked Unique Bush buds are nowhere.
The laced plant bundle is a thing. The laced plant bundle is nowhere.
The tied plant bundle is a thing. The tied plant bundle is nowhere.
The prepared medicine is a thing. The prepared medicine is nowhere. The description is "It has a terrible odor. A great headache cure."

Preparing is an action applying to one carried thing. Understand "prepare [something]" as preparing.

Preparing it with is an action applying to two carried things. Understand "prepare [something] with [something]" as preparing it with. Understand "prepare [something] in [something]" as preparing it with.

Understand "prepare medicine" as a mistake ("Try preparing each ingredient according to the spell.") when the papyri is examined.

Carry out preparing:
	if the noun is the buds of a unique bush:
		now the buds of a Unique Bush is nowhere;
		now the player is holding the twisted Unique Bush buds;
		say "You take the buds of the Unique Bush and carefully twist them leftwise.";
		say "You now have [twisted Unique Bush buds].";
	otherwise if the noun is the twisted Unique Bush buds:
		if the player is not holding the mucus:
			say "You can't continue preparing the medicine if you don't have the mucus. The next step is soaking the buds in mucus.";
		otherwise:
			say "You submerge the twisted buds in mucus until they are thoroughly soaked.";
			now the twisted Unique Bush buds is nowhere;
			now the mucus is nowhere;
			now the player is holding the soaked Unique Bush buds;
			say "You now have [soaked Unique Bush buds].";
	otherwise if the noun is the soaked Unique Bush buds:
		if the player is not holding a bud of a snb-plant:
			say "You can't continue preparing the medicine without the bud of a snb-plant. You need both the soaked Unique Bush buds and the bud of the snb-plant to lace them together.";
		otherwise:
			say "You take the bud of the snb-plant and lace it together with the soaked Unique Bush buds.";
			now a bud of a snb-plant is nowhere;
			now the soaked Unique Bush buds is nowhere;
			now the player is holding the laced plant bundle;
			say "You now have [laced plant bundle].";
	otherwise if the noun is the laced plant bundle:
		say "Using some string, you tie the laced plant buds together with seven evenly spaced knots.";
		now the laced plant bundle is nowhere;
		now the player is holding the prepared medicine;
		say "You now have [the prepared medicine].";
	otherwise:
		say "That's not the right thing to prepare."
		
Carry out preparing the something with something:
	if the noun is the twisted Unique Bush buds:
		if the second noun is the plant mucus:
			say "You submerge the twisted buds in mucus until they are thoroughly soaked.";
			now the twisted Unique Bush buds is nowhere;
			now the mucus is nowhere;
			now the player is holding the soaked Unique Bush buds;
			say "You now have [soaked Unique Bush buds].";
		otherwise:
			say "That's not the right thing to prepare twisted unique bush buds with.";
	otherwise if the noun is the soaked Unique Bush buds:
		if the second noun is the bud of a snb-plant:
			say "You take the bud of the snb-plant and lace it together with the soaked Unique Bush buds.";
			now a bud of a snb-plant is nowhere;
			now the soaked Unique Bush buds is nowhere;
			now the player is holding the laced plant bundle;
			say "You now have [laced plant bundle].";
		otherwise:
			say "That's not the right thing to prepare the [soaked unique bush buds] with.";
	otherwise if the noun is the bud of a snb-plant:
		if the second noun is the soaked Unique Bush buds:
			say "You take the bud of the snb-plant and lace it together with the soaked Unique Bush buds.";
			now a bud of a snb-plant is nowhere;
			now the soaked Unique Bush buds is nowhere;
			now the player is holding the laced plant bundle;
			say "You now have [laced plant bundle].";
		otherwise:
			say "That's not the right thing to prepare the [soaked unique bush buds] with.";
	otherwise:
		say "That's not the right thing to prepare."
		
		
Understand "twist [buds of a unique bush]" or "twist [buds of a unique bush] left" or "twist [buds of a unique bush] leftwise" as preparing.

Understand "soak [twisted Unique Bush buds]" as preparing.

Understand "soak [twisted Unique Bush buds] in [mucus]" or "soak [twisted Unique Bush buds] with [mucus]" as preparing it with.

Understand "lace [soaked Unique Bush buds]" as preparing.

Understand "lace [soaked Unique Bush buds] with [bud of a snb-plant]" as preparing it with.

Understand "lace [bud of a snb-plant] with [soaked Unique Bush buds]" as preparing it with.

Understand "fit [laced plant bundle] with 7 knots" or "fit [laced plant bundle] with seven knots" or "knot [laced plant bundle]" or "knot [laced plant bundle] 7 times" or "knot [laced plant bundle] seven times" as preparing.

Understand "apply [other things] on [something]" as putting it on.

Understand "apply [other things] to [something]" as putting it on. [("Try applying it ON [the second noun]")]

Heracles can be medicated or unmedicated. Heracles is unmedicated. 

Instead of putting the prepared medicine on Heracles:
	say "You gently place the prepared bundle on the throat of the afflicted Heracles. You should recite the spell now.";
	now Heracles is medicated.
	
Understand "heal [Heracles]" or "help [Heracles]" or "cure [Heracles]" as a mistake ("[if the player is carrying the prepared medicine]You gotta apply the medicine and get down and dirty with the nitty gritty steps! [otherwise if the papyri is examined]Try following the steps in the spell. A sick Heracles can't be healed in one step, after all.[otherwise]You don't even know where to start! Now that you can't use your magic, you have to start reading up on the mortal way of things.").
	
Understand "I have indeed applied something to all his sore spots" or "say I have indeed applied something to all his sore spots" or "say 'I have indeed applied something to all his sore spots'" or "'I have indeed applied something to all his sore spots'" as chanting. Chanting is an action applying to nothing.

Instead of answering that "spell", say "Try saying out the full spell.".

Understand "recite spell" or "say spell" as a mistake ("Try saying out the full spell.") when the papyri is examined.

Carry out chanting:
	say "You feel a little crazy muttering to yourself."

Carry out chanting in the presence of sick Heracles:	
	say "You speak the spell.[paragraph break]";
	wait for any key;
	say "You wait for something to happen.[paragraph break]";
	wait for any key;
	say "...[paragraph break]";
	wait for any key;
	say "Still waiting...[paragraph break]";
	wait for any key;
	say "...[paragraph break]";
	wait for any key;
	say "In a rush, Heracles jumps up!  'WOAH!! I feel better!' he roars.[paragraph break]";
	wait for any key;
	say "'I must give you my greatest thanks, dear friend!' He turns to you. 'AHHHHHH!!!! MEDEA!!!!!!' [paragraph break]";
	wait for any key;
	say "You patiently wait for him to process the shock. [paragraph break]";
	wait for any key;
	say "'Ah-ah-ah-but!! You!!!!! I won't let you harm anyone!!!!! I'll stop you!!!' [paragraph break]";
	wait for any key;
	say "'...You have little to fear,' you reply. 'I'm on probation.' [paragraph break]";
	wait for any key;
	say "Heracles calms down and takes stock of the situation. 'Is that so? Well...so long as you promise not to do it again.' [paragraph break]";
	wait for any key;
	say "Looking gloomy, he continues. 'I myself know a little about hurting one's family.' [paragraph break]";
	wait for any key;
	say "His sad expression is replaced by determination. 'Well! I'll follow you around--to keep an eye on you, of course!' [paragraph break]";
	wait for any key; 
	say "Under his breath, he whispers, 'I'm also totally lost...' [paragraph break]";
	now Heracles is healthy.

Section 4 - After the heart

[have dialogue of medea putting heart in bag, and osiris' head coming to life.]
[fix activation of scenes]

Chapter 4 - Busiris-Organs

Organ Acquisition is a scene. Organ Acquisition begins when Heal Heracles ends. Organ Acquisition ends when Osiris' liver is acquired and Osiris' lungs is acquired and Osiris' stomach is acquired and Osiris' intestines is acquired.

Instead of entering the chariot during Organ Acquisition:
	say "You really didn't want Heracles' filthy paws anywhere near your beautiful chariot.".

Table of Countdown
num_moves
1
2
3
4
5
6
7
8
9
10

Table of Heracles' Yapping
yappery
"'Why is every single temple so far apart?' Heracles shouts. 'My heroic feet can't take this abuse! It was so much better in Greece!' [paragraph break]People in the distance start whispering at the commotion...[paragraph break]"
"'So bored... My old battles were never this slow,' Heracles whines. 'Someone give me a monster or something to smash!'[paragraph break]"
"Heracles bellows, 'Why does the sun have to be so bright? Someone extinguish it!' His voice booms across the area, turning every head."
"'This humidity is a mighty adversary,' Heracles complains. 'Medea! Use your Colchian magic to fix it!'[paragraph break]As he speaks, you notice a few people looking at you...[paragraph break]"
"Heracles scratches his head. 'My head’s still a little ouchie. Could you—like, I dunno—magic it away?'[paragraph break]"
"'Thanks again for your help,' Heracles says. 'Athough I totally had it handled.[paragraph break]"
"'Have you ever tried cleaning out stables for a living?' Heracles complains. 'Makes any quest seem easy.'[paragraph break]"
"'I just have to ask,' Heracles loudly says. 'What made you do it?' [paragraph break]You give no reply..[paragraph break]"
"Heracles groans, clutching his temples. 'All this talk of gods and brothers and chopping...Egypt is so strange! I'd rather wrestle another lion!!'[paragraph break]His loud voice draws a few curious eyes....[paragraph break]" 

Every turn during Organ Acquisition:
	if the Table of Countdown is empty:
		do nothing;
	if the Table of Countdown is not empty:
		choose a random row in Table of Heracles' Yapping;
		say "[yappery entry]";
		if the location is not the House of Life:
			repeat through Table of Countdown:
				blank out the whole row;
				rule succeeds.

The Kidnapping is a scene. The Kidnapping begins when the number of filled rows in the Table of Countdown is 0. The Kidnapping ends when King Busiris is dead.

When the Kidnapping begins:
	say "Suddenly a gang of men rush at the two of you, catching you by surprise!![paragraph break]";
	wait for any key;
	say "Despite your struggles, they knock you unconscious and drag you away.You've been kidnapped!![paragraph break]";
	wait for any key;
	say "You wake up bleary-eyed in a dark, musty prison cell.[paragraph break]";
	wait for any key;
	say "Divine jail was much more luxurious.[paragraph break]";
	wait for any key;
	say "Heracles, also coming to consciousness, is lying beside you. 'Medea!' he says. 'Where are we?! What happened!? Did you do—?!'[paragraph break]";
	wait for any key;
	say "He's interrupted by the menacing laughter of a man standing outside of your cell. His ornate robes are stained at the hem with dried blood. [paragraph break]";
	wait for any key;
	say "'That weak woman? Are you mad, man?' he says. 'No! It was I, Busiris!' [paragraph break]";	wait for any key;
	say "You and Heracles stare at him in confusion. You have no idea who that is. [paragraph break]";
	wait for any key;
	say "The mysterious man--Busiris, you guess--glares at you. 'Seriously?' he asks. 'You don't know who I am?! I'm in at least three different ancient books!!'[paragraph break]";
	wait for any key;
	say "'Sorry, dude,' Heracles replies. 'We have no clue who you are.' [paragraph break]";
	wait for any key;
	say "'Don't you call me 'dude,'' Busiris says. 'That would make me want to kill you...if I weren't already going to kill you!!'[paragraph break]";
	wait for any key;
	say "What??? [paragraph break]";
	wait for any key;
	say "'Yes!' Burisis continues, pleased by your wariness. 'Shedding foreign blood is an important part of my city's social culture! And you have only that loudmouth to thank for your capture! Normally we have a 35% success rate in successfully capturing a foreigner--but thanks to that blabbermouth, we're certain you're Greek!' [paragraph break]";
	wait for any key;
	say "You turn to Heracles, giving him a nasty look. He looks at you sheepishly. 'Sorry...' [paragraph break]";
	wait for any key;
	say "'I'll let you two work that out,' Busiris says smugly. 'But don't take too long in making amends! It's never good to die angry!' [paragraph break]";
	wait for any key;
	say "With that, he leaves you two alone. But before you can give Heracles a piece of your mind, he says something. [paragraph break]";
	wait for any key;
	say "'Umm..Medea...the gaps of the bars are pretty wide! I think we can slip through them!' [paragraph break]";
	wait for any key;
	now the player is in the Jail Cell;
	now Heracles is in the Jail Cell.

[change it so you have to talk to heracles to leave the jail cell the first time]


Section 1 - Escape Management

Escape is a scene. Escape begins when the location is the Jail Cell and Osiris' liver is unacquired and Osiris' lungs is unacquired and Osiris' stomach is unacquired and Osiris' intestines is unacquired. Escape ends when Heracles is caught. Escape is a recurring scene.

Heracles can be caught or uncaught. Heracles is uncaught.

Rule for printing the locale description of Heracles during Organ Acquisition:
	do nothing.

The invisible tool is a thing. 

The hallway count is a number that varies. The hallway count is initially 0.
		
After going to the Hallway during Escape:
	increment hallway count;
	showme hallway count;
	if hallway count is 5:
		now the hidden entrance is revealed;
		now the hidden entrance is open;
		say "A loud rumbling echoes through the hallway, unveiling a hidden passageway to your east.";
	continue the action.

The visit-jail count is a number that varies. The visit-jail count is initially 0.

When Escape begins:
	if the visit-jail count is not 0:
		say "[notice style]You have been taken by the guards into a jail cell![end style]";
	now Heracles is uncaught;
	increment visit-jail count;
	showme visit-jail count.
	
Understand "talk to guards" or "talk to guard" as a mistake ("The guards have left to patrol the palace.").
	
When Escape ends:
	say "[notice style]The guards rush into the room and apprehend you![end style]";
	wait for any key;
	now Heracles is uncaught;
	now Heracles is in the Jail Cell;
	now the player is in the jail Cell.
	
Every turn during Escape:
	if Osiris' liver is acquired and the location is not Crypt of Osiris:
		now Heracles is caught;
	if the location is Central Court:
		if a random chance of 1 in 2 succeeds:
			now Heracles is caught;			

Instead of going when the location is a corridor during Escape:
	if a random chance of 1 in 6 succeeds:
		say "[paragraph break][notice style]You amble down the winding corridors...[end style][paragraph break]";
		wait for any key;
		teleport the player;	
	otherwise:
		continue the action.
		
To teleport the player:
	choose a random row in the Table of Corridor Confusion;
	now the player is in the corr_name entry;
	now Heracles is in the corr_name entry;
	say "'How did we get here?' Heracles wonders loudly.".
	
After going during Escape:
	if the location is not a corridor:
		[say "timer set";]
		set the timer;
	otherwise:
		[say "delay timer";]
		delay the timer;
	continue the action.
	
To set the timer:
	the invisible tool rings in 3 minutes from now.
	
To delay the timer:
	the invisible tool rings in one hour from now.
	
At the time when the invisible tool rings:
	if the location is a place listed in the Table of Dangerous Places:
		[say "ring, this is a possible place";]
		if a random chance of 1 in 3 succeeds:
			now Heracles is caught;
		
		
Table of Dangerous Places
place (a room)
Pillar Hall
Temple Repository
Pillar Crypt
Shrine Room
Double Axe Hall 
West Magazines
South Propylaeum
Workshops
Court of the Stone Spout
Lobby
Tricolumnar Hall
Treasury
Great Hall-Busiris
Porch

Table of Corridor Confusion
corr_name (a room)
Corridor of Procession East
Corridor of Procession West
Hallway
North Corridor
Corridor of the Draught Board
Upper Hallway

Section 2 - Busiris Palace

Central Court is a room in Busiris. "Center of public life in Busiris."

King Busiris is a person. King Busiris can be alive or dead. King Busiris is alive. The description of dead King Busiris is "Dead, just like all your enemies were."

A corridor is a kind of room. 

Pillar Hall is a room in Busiris. It is north of Central Court. Pillar Hall is northeast of Hallway. "The air in this vaulted chamber seems to thrum with a sinister power."

Temple Repository is a room in Busiris. It is north of Pillar Crypt. "Rows of dusty shelves crammed with scrolls, clay tablets, and magical texts."

The Crypt of Osiris is a room in Busiris. It is east of the hidden entrance. West of Crypt of Osiris is nothing. "Cloaked in a heavy silence, this chamber radiates an eerie chill. It's clearly been flooded at some point and never dried, a perpetual dampness permeating every corner and crevice of the room."

The hidden entrance is a secret door. The hidden entrance is east of Hallway. "A faint outline on the stone wall indicates a hidden door!"

Pillar Crypt is a room in Busiris. It is west of Central Court. It is east of Crypt of Osiris. Nothing is west of Pillar Crypt. The description is "A crypt with pillars in it.".

Corridor of Procession East is a corridor in Busiris. It is south of Central Court. "A broad ceremonial passage with faded murals, now haunted only by the sound of your footsteps."

Shrine Room is a room in Busiris. It is east of Corridor of Procession. "The dim torchlight illuminates the altars dedicaed to various deities."

Double Axe Hall is a room in Busiris. It is north of the Shrine Room. It is east of Central Court. "Huge double-axe motifs dominate the walls here."

Corridor of Procession West is a corridor in Busiris. It is west of Corridor of Procession East. "Grand arches line this corridor’s high ceiling, but time and neglect have left cracks in its structure."

Palace exit is a secret door. It is west of Corridor of Procession West and east of the Temple of Tawosret. 

Hallway is a corridor in Busiris. It is north of Corridor of Procession West. "Its walls are streaked with dark soot and grime. This place doesn't have very good upkeep."

West Magazines is a room in Busiris. It is west of Hallway. "Long, narrow rooms crammed to the brim with hundreds of [set-link pithoi]. You can hardly move around with all of these jars blocking your way. It's clearly a fire hazard, and you would know about those. There is a [set-link papyrus scrap] lying on the ground."

The pithoi are plural-named. The pithoi are in the West Magazines. The description of the pithoi are "Jars filled with olive oil, wine, and other delicacies. Enough food to fuel an empire, it seems. Or fund a feast fit for a king. It seems that you (and Heracles) won't go hungry." The print-name is "pithoi".

Instead of taking pithoi:
	say "They're much too heavy to carry."

The abandoned medical papyrus scrap is in the West Magazines. The description of the abandoned medical papyrus scrap is "A crumpled and yellowing scrap of papyrus. It reads: 'A liver that is too full of blood can produce all diseases'. Truly salient advice. You ought to bring it up at your next check-up.". The print-name is "papyrus scrap".

Instead of examining Heracles in the West Magazines:
	say "Heracles seems a little nervous."
	
Instead of talking to Heracles in the West Magazines:
	say "'Um, Medea.' Heracles looks more antsy than usual. 'I don't like being around all these jars. They're all so smashable!' ...He's like a boar in a pithoi shop."

South Propylaeum is a room in Busiris. It is southwest of Central Court. It is northeast of Corridor of Procession West. It is northwest of Corridor of Procession East. "Massive gateways stand here, their once-intricate reliefs chipped away by time."

North Corridor is a corridor in Busiris. It is east of Pillar Hall. "A narrow hall with walls lined by cracked paintings."

Corridor of the Draught Board is a corridor in Busiris. It is south of North Corridor. "A long and winding corridor."

The Jail Cell is a room in Busiris. It is west of the cell door.  "Dark and claustrophobic. You can see desperate scratches of former prisoners."

The cell door is a locked open door. The cell door is east of the Jail Cell and west of the Corridor of the Draught Board. 

Rule for writing a paragraph about the cell door:
	stop the action.

Workshops is a room in Busiris. It is east of the Corridor of the Draught Board. "You see workbenches and tools scattered about."

Court of the Stone Spout is a room in Busiris. It is south of Workshops. It is north of Double Axe Hall. It is southeast of the Corridor of the Draught Board. "A small courtyard where a carved spout juts from the wall."

Lobby is a room in Busiris. It is above the South Propylaeum. "Another room. But where is the exit?"

Tricolumnar Hall is a room in Busiris. It is north of Lobby. "Three giant pillars fill this hall, casting long shadows across the floor."

Upper Hallway is a corridor in Busiris. It is west of Tricolumnar Hall and northwest of Lobby.  "Another corridor. You've having some trouble keeping track of where you are." 

Treasury is a room in Busiris. It is east of Lobby. "Treasury of the Sanctuary, so called."

Great Hall-Busiris is a room in Busiris. It is west of Upper Hallway. The printed name of Great Hall-Busiris is "Great Hall".

Porch is a room in Busiris. It is north of Great Hall-Busiris. Porch is northwest of Upper Hallway. The description is "Looking out at the west pavilion of the palace."

	
Section 3 - Organ Minigame
	
A canopic jar is a kind of container. It is openable, closed, and opaque. 
A canopic jar can be identified or unidentified. A canopic jar is usually unidentified.


Instead of taking a canopic jar:
	say "A large alabaster vessel does not fit in with your escape plans.".

Instead of taking something which is in a canopic jar:
	say "The putrid waters did not look inviting to stick your hand in. Maybe you could try scooping it instead, like soup.".
			
Report opening a unidentified canopic jar:
	say "As you open the jar, a horrific smell reaches you. Gag. The sight inside does not help.".
			
Instead of smelling a canopic jar:
	say "Gag.".
	
Instead of examining something which is in a canopic jar:
	if the noun is in a unidentified canopic jar:
		say "Better not take somthing you don't know.";
	
Instead of examining a canopic jar:
	if the noun is closed:
		try silently opening the noun;
	say "[the description of the noun][paragraph break]";
	omit contents in listing.

The description of a canopic jar is "[if identified]You know what's inside.[otherwise]Unknown Goop, mushy and waterlogged.".

Rule for printing the locale description of the Crypt of Osiris:
	say "You can see an open ";
	place a link to the command "x stone chest" reading "stone chest";
	say " sitting atop a pile of sand, its ";
	place a link to the command "x lid" reading "lid";
	say " halfway down the mound. Inside the chest are four closed jars, carefully arranged to each face ";
	place a link to the command "x north jar" reading "north";
	say ", ";
	place a link to the command "x east jar" reading "east";
	say ", ";
	place a link to the command "x south jar" reading "south";
	say ", and ";
	place a link to the command "x west jar" reading "west";
	say "." instead.


The canopic chest is a container in the Crypt of Osiris. It is open, openable, and scenery. Understand "stone chest" as the canopic chest. The print-name is "stone chest". The description is "With a closer look, you find that it's actually a canopic chest.".

The north canopic jar, east canopic jar, south canopic jar, and west canopic jar are canopic jars in the canopic chest.

The description of north jar is "[if identified]Inside it was, appararently, Osiris' lungs.[otherwise]Unknown Goop north, mushy and waterlogged.".
			
The description of east jar is "[if identified]Inside it was, appararently, Osiris' stomach.[otherwise]Unknown Goop east, mushy and waterlogged.".
		
The description of south jar is "[if identified]Inside it was, appararently, Osiris' liver.[otherwise]Unknown Goop south, mushy and waterlogged.".
		
The description of west jar is "[if identified]Inside it was, appararently, Osiris' intestines.[otherwise]Unknown Goop west, mushy and waterlogged.".
			
Understand "examine unkown goop" or "examine goop" or "x unknown goop" or "x goop" as a mistake ("You squint at the goop, willing your eyes to infer some shape from it. Nothing forms.") when the location is the Crypt of Osiris.
			
Understand "take unkown goop" or "take goop" as a mistake ("Better not take somthing you don't know.") when the location is the Crypt of Osiris.		
Osiris' lungs is a body part. It is in the north jar. Understand "lung" or "lungs" as Osiris' lungs. The description is "An overly healthy pair of lungs. You didn't like it all that much."
	
Osiris' stomach is a body part. It is in the east jar. The description is "A stomach. Perhaps for digestion."
	
Osiris' liver is a body part. It is in the south jar. It is unacquired. Understand "liver" as Osiris' liver. The description is "A liver. It is surprisingly spongy."

Osiris' intestines is a body part. It is in the west jar. Understand "intestine" as Osiris' intestines. The description is "Long. You wonder what it would looks like turned into sausage."
	
Understand "take lung" as a mistake ("Osiris would complain endlessly if you only took one lung.") when the location is the Crypt of Osiris

A Bes Jar is in the Crypt of Osiris. It is unacquired. The description of the Bes Jar is "A vase decorated with the head of Bes. You see traces of hallucinogens in it, along with...breast milk and blood?"

Scooping is an action applying to two things. Understand "scoop [something] from [something]" or "ladle [something] from [something]" as scooping.
	
Carry out scooping:
	if the player is not carrying the kyathos:
		say "Your non-webbed fingers make a poor tool for scooping.";
	otherwise:
		if the second noun is not a container:
			if the second noun is the kyathos:
				say "That's what you're scooping [italic type]with[roman type]. Scoop-ception is not allowed!";
			otherwise:
				say "It hardly makes sense to scoop from [second noun].";
		otherwise if the noun is not in the second noun:
			say "You may be fishing for something that's not there.";
		otherwise:
			if the second noun is a canopic jar:
				now the second noun is identified;
				now the player is carrying the noun;
				say "You swirl the kyathos in the gross mixture, and aha! You are now in possession of the most delicious snack: a [noun]!.".
	
[kyathos should not be in crypt of osiris. to be located somewhere that makes more sense]

A silver kyathos is in the Crypt of Osiris. The description is "A silver cup-shaped ladle, with a duck's head at the end of the handle." 

	
	
	
Section 4 - Ritual Sacrifice

Ritual Sacrifice is scene. Ritual Sacrifice begins when Osiris' liver is acquired. Ritual Sacrifice ends when King Busiris is dead.

[fix the slow capture before they get taken to the sacrifice]
Instead of going during Ritual Sacrifice:
	if Escape is not happening:
		say "You try to leave, but the guards make a grab for you!";
	otherwise:
		continue the action.

Every turn during Ritual Sacrifice:
	If the location is the Jail Cell:
		start the sacrifice;

[another option for suppressing reporting of central court is: "move the player to the Central Court, without printing a room description."]

Instead of looking in the Central Court when Ritual Sacrifice is happening:
	say "[run paragraph on]";
	
To start the sacrifice:
	say "'The ritual is ready. We will finally deal with you errant prisoners! Our gods will certainly be pleased with this sacrifice,' a guard jeers.[paragraph break]";
	wait for any key;
	now the player is in the Central Court;
	now Heracles is in the Central Court;
	say "Without further ado, you and Heracles are dragged to the central court, kicking and screaming.[paragraph break]";
	wait for any key;
	say "You can see that preparations for the sacrifice has been complete. A massive stone altar stands at the center, surrounded by flickering torches and gleeful guards.[paragraph break]";
	wait for any key;
	now King Busiris is in the Central Court;
	wait for any key;		
	say "And standing in front of the altar is Busiris, with a sinister smile on his face. [paragraph break]";
	say "Busiris steps onto a raised dais in the center of the central court. [paragraph break] 'Fools! he booms, his voice echoing off the high stone walls. 'You stand accused by the gods themselves! Fear not--your foreign blood will be a worthy sacrifice!' He throws his head back and cackles with mad delight.[paragraph break]";
	wait for any key;
	say "Around you, a circle of guards brandish their weapon--no easy escape.[paragraph break] ";
	wait for any key;
	say "You desperately try to make eye contact with Heracles, who stands rigid at your side. Your usual cunning won't be enough to save you here--and with your divine powers suppressed by the Egyptian gods, there's nothing you can do![paragraph break] "; 
	wait for any key;
	say "You're used to handling all schemes yourself. But you might need to rely on Heracles here...maybe you can whisper instructions to him while Busiris continues rambling.[paragraph break] ";
	wait for any key;
	
Rule for listing exits when the location is Central Court during Ritual Sacrifice: 
	do nothing.

Instead of talking to Heracles in the presence of King Busiris during Ritual Sacrifice:
	say "If you're too loud, Busiris will hear you...it's better to whisper."

Whispering to is an action applying to one visible thing.
Understand "whisper to [someone]" or "whisper instructions to [someone]" as whispering to.

[fix talking to problem - ensure player is in central court when this happens - should be ok, double check]
Instead of whispering to Heracles in the presence of King Busiris during Ritual Sacrifice:
	end the sacrifice.
	
To end the sacrifice:
	say "'Psst! Heracles!' you whisper. [paragraph break]";
	wait for any key;
	say "Heracles turns to you with a terrible dread on his face. 'Medea, I fear this is where my story ends.' [paragraph break]";
	wait for any key;
	say "You power through: 'Nevermind that!! I know that you can break through these flimsy shackles. Escape now and stop Busiris while he's distracted!' [paragraph break]";
	wait for any key;
	say "Heracles looks at you with awe. 'Why, Medea! You're a genius!' [paragraph break]";
	wait for any key;
	say "You know. [paragraph break]";
	wait for any key;
	say "When you give him the cue, Heracles springs forward with a mighty roar. Busiris turns, but it's too late. The tyrant crumples to the ground, his final scream lost in the sound of the sudden uproar.[paragraph break]";
	wait for any key;
	say "Perfect. Why dirty your hands when someone can do it for you? [paragraph break]";
	wait for any key;
	now King Busiris is dead;
	say "The court descends into total chaos, and the guards are rushing about in confusion. Now is a good time to find your way out! Try looking for a palace exit!";
	now palace exit is revealed;
	now palace exit is open;
	wait for any key;

The description of palace exit is "An imposing door by the Corridor of Procession West. With all the chaos, this might be your chance!"

Instead of going through palace exit:
	now the player is in The Temple of Tawosret;
	now Heracles is in the Temple of Tawosret;
	wait for any key;
	trigger Heracles leaving.
	
[player carrying capacity is ok, restriction only applies to players ]

To trigger Heracles leaving:
	say "After escaping from that fiasco, Heracles turns to you. 'Dearest Medea!' He beams at you. 'How glorious it was to adventure with you attending to me! You have a very promising career as a sidekick ahead of you.'[paragraph break]";
	wait for any key;
	say "You remain silent, although you're not sure if that's because of self-restraint, accustomation to his antics, or just pure exhaustion.[paragraph break]";
	wait for any key;
	say "'I truly must thank you for your help,' Heracles says, 'and, of course, for healing me with your witchy powers.[paragraph break]";
	wait for any key;
	say "I've heard stories about you, but it seems that one should never judge a hero (or heroine) by his (or her) tragedies--especially those written by that snooze Euripides!'[paragraph break]";
	wait for any key;
	say "...He could have just said 'their.'[paragraph break]"; 
	wait for any key;
	say "'I'll be off now to pursue my own quests,' Heracles continues. 'But I hope you'll take this trinket as a token of my gratitude. Until we meet again! Perhaps in a different Thebes...'[paragraph break]";
	wait for any key;
	say "You now have a heart scarab made of shining carnelian. Maybe you can examine it more closely. And with that, Heracles leaves.[paragraph break]";
	wait for any key;
	now Heracles is nowhere;
	say "He certainly was obnoxious, but it was nice to travel with a companion.";
	now the player is holding the heart scarab;
	now the heart scarab is acquired;
	now palace exit is unrevealed;
	now palace exit is closed.

Part 5 - Complete Any Time
[can be completed any time after acquiring the head]

Hand Acquisition is a scene. Hand Acquisition begins when Head Acquisition ends. Hand Acquisition ends when Osiris' left hand is acquired and Osiris' right hand is acquired.

Chapter 5 - Avaris-Hands

Osiris' left hand is a body part. It is in Avaris-Marketplace.

Osiris' right hand is a body part. It is in Avaris-Marketplace.


Part 6 - Tour Guide Osiris
[this part can be completed out of order, although some chapters require objects from other chapters to be completed]

Tour Guide Osiris is a scene. Tour Guide Osiris begins when Talking Osiris Agenda ends. [doesn't really end]

Chapter 6 - Oxyrhynchos

Penis Acquisition is a scene. Penis Acquisition begins when Tour Guide Osiris begins. Penis Acquisition ends when Osiris' penis is acquired.

Section 1 - Cutting as an action

A blade is a kind of thing.
		
Cutting it with is an action applying to two carried things. Understand "cut [something] with [something preferably held]" as cutting it with. Understand "gut [something] with [something preferably held]" as cutting it with. 

Check cutting something with something:
	if a blade is not carried:
		say "You can hardly cut open the fish without a proper tool.";
	if the noun is a blade, say "You can't do that." instead;
	if we have cut the noun:
		say "The fish hardly deserves to be gutted twice."

Instead of cutting something:
	if a blade (called the edge) is held by the player,
		try cutting the noun with the edge;
	otherwise say "Your fingernails are not sharp enough."

Check cutting it with:
	if the noun is a person, say "That would hurt." instead;
	if the second noun is not a blade, say "[The second noun] has not got enough of a blade." instead.

Carry out cutting it with:
	increment the count of rips of the noun.

Report cutting it with:
	say "You slash [the noun] with [the second noun]."

[We'll need a way to account for all these cuts and rips.]

Definition: a thing is ripped if the count of rips of it > 0.  A thing has a number called the count of rips. After examining something ripped, say "You see [the count of rips of the noun in words] rip[s] in [the noun][if something is in the noun], revealing [a list of things in the noun][end if]."

The examine containers rule does nothing when examining the catfish.

Carry out cutting a container with something:	
	now the noun is open;
	now the noun is unopenable.

[Now our rule will occur whenever a container is cut, but play will still go on to the reporting stage. And indeed we can add more of these, of varying degrees of specificity:]

Carry out cutting something which is part of something with something:
	move the noun to the player.

[For that matter, we might want to add a report rule as well, to occur after the "You slash..." rule, so that every time the player cuts something open which has contents, the contents will be listed.]

Report cutting it with:
	if the noun is open and the noun contains something,
		say "Visible within [is-are a list of things in the noun]."

[Here is a final nicety to get rid of the "which is closed" statement on our closed unopenable container, using an "activity" rule:]
After printing the name of a closed unopenable container:
	omit contents in listing.
	
Instead of cutting a body part:
	say "You'll hardly win any favors by cutting Osiris into even more pieces.".

Section 2 - Marketplace

Oxyrhynchos-Marketplace is a room in Oxyrhynchos. "(You have reached a part of the game that is still under development. This section is partially complete. Should you want to try the minigame, please take all the items at the market place and head to the riverbank.)  [paragraph break] You are in Oxyrhynchos, home of the medjed elephantfish. A little further east is the riverbank of the Nile.  [if the sun chariot is in the location]Your [sun chariot] is parked nearby.[end if]"



[include description of market place and items here.]

[include meeting a couple with a pregnant woman nearing birth time. she is looking for a good luck pendant, but they are too poor to afford it.]

Some bait is an edible thing in Oxyrhynchos-Marketplace.

Instead of eating the bait:
	say "Edible, sure, but you're no fish.".

A fishing rod is in Oxyrhynchos-Marketplace. The fishing rod can be baited or unbaited. It is unbaited. The printed name of the fishing rod is "fishing rod[if fishing rod is baited] with bait". The description is "[if fishing rod is unbaited] A sturdy fishing rod, but it could use some bait.[end if] [if fishing rod is baited] The fishing pole is now baited with some fish food."

A knife is a blade in Oxyrhynchos-Marketplace. 

Section 3 - Tawaret conflict

The Nile Riverbank is in Oxyrhynchos. It is east of Oxyrhynchos-Marketplace. "You are at the bank of the Nile. West of you is the marketplace."

The Taweret Amulet is a thing. 

Tawaret Conflict is a scene. Tawaret Conflict begins when Penis Acquisition begins. Tawaret Conflict ends when the Taweret Amulet is acquired.

[add lots of dialogue and the whole conflict relating to tawaret]

[Ruya can do the Tawaret character and all associated interactions with hippos]

Section 4 - Fishing game

[maybe change the description of the Nile once Tawaret is gone. mention the fish in it]

A species is a kind of value. The plural of species is species. The species are perch, tilapia, mormyrid, and tigerfish. 

A fish is a kind of container. A fish are edible. A fish is closed. A fish can be found in the Nile Riverbank. The plural of fish is fish. A fish has a species. The species of a fish is usually tilapia. The description of a fish is usually "A fish." Understand the species property as describing a fish.

In the Nile Riverbank there is 10 fish.

After printing the name of a fish:
	omit contents in listing.

A fish gut is a kind of thing. The description of fish guts is "Fish guts." Fish gut is in every fish.

Rule for printing the name of fish gut:
	say "fish guts".

Before listing contents: group fish together.

Rule for printing the name of a fish:
	say "[species]".

[Rule for printing a number of fish (called the target): 
	if the listing group size is less than 4:
		say "a few "; 
	else if the listing group size is less than 6:
		say "some ";
	else if the listing group size is less than 8:
		say "a bunch of ";
	else:
		 say "many ";
	carry out the printing the plural name activity with the target.]

A catfish is a closed container. The description of the catfish is "[if player is not carrying catfish] It's a catfish... I wonder if that has any significance? [end if] [if player is carrying closed catfish] You eye the catfish with great suspicion. It looks back at you and reveals nothing. Maybe its silence can be resolved by a good gutting.[end if] [if catfish is open] It's been gutted. Poor catfish."

Instead of taking a fish:
	say "The fish are swimming freely in the Nile. Maybe you could try fishing for it."

Instead of eating the catfish:
	say "You look at it with immense distrust. It just looks so awfully... fishy... Better not.".
	

Osiris' penis is a body part in catfish. Understand "penis/phallus" as Osiris' penis.
	
Baiting is an action applying to one carried thing. Understand "bait [something]" as baiting.

Before baiting something:
	if the player is not carrying the noun, say "You don't have that." instead;
	if the noun is not the fishing rod, say "You can't bait that." instead.

Carry out baiting:
	if the fishing rod is unbaited:
		if the player is carrying bait:
			say "You attach the bait carefully to the fishing rod." ;
			now the bait is nowhere;
			now the fishing rod is baited;
		otherwise:
			say "You don't have anything to bait it with.";
	otherwise:
		say "More is not always better."
			
Fishing is an action applying to nothing. Understand "fish" or "go fishing" or "go fish" as fishing.

Before fishing:
	if the fishing rod is not carried:
		 say "You've got nothing to fish with. Your hands won't suffice, unfortunately." instead;
	otherwise if the fishing rod is unbaited:
		say "You won't catch much without bait on the hook."  instead;
	otherwise if player is not in Nile Riverbank:
		say "This is not a viable place to be fishing." instead;
[	otherwise if player is carrying the catfish:
		say "You already have a fish in your posession. Don't be so greedy." instead;]
	otherwise:
		say "You cast your fishing rod out with a strong throw..."
		
		
Instead of dropping a fish:
	now the noun is nowhere;
	say "You drop the fish. It makes a valiant leap for the river and succeeds! It swims away aggressively."
		
Carry out fishing:
	if a random chance of 1 in 5 succeeds:
		move the catfish to the player;
		say “You have caught the catfish!”;
	otherwise:
		let X be a fish;
		unless X is nothing:
			now the species of X is a random species;
			move X to the player;
			say “You have caught a [species of X].”
		
		
[Carry out fishing:
	if a random chance of 1 in 5 succeeds:
		move the catfish to the player;
		say “You have caught the catfish!”;		
		now the fishing rod is unbaited;
	otherwise:
		say “And pulled up nothing.”;]

Test fish with "take rod / take knife / take bait / bait rod / e" in Oxyrhynchos-Marketplace.
			
Chapter 7 - Elephantine-Torso

Osiris' Torso is a body part in Elephantine-Marketplace. 

Chapter 8 - Memphis-Spine

Osiris' Spine is a body part in Memphis-Marketplace.

Chapter 9 - Heliopolis-Limbs

Osiris' right arm is a body part in Heliopolis-Marketplace. "His right arm, missing its hand."

Osiris' left arm is a body part in Heliopolis-Marketplace. "His left arm, missing its hand."

Osiris' right leg is a body part in Heliopolis-Marketplace.

Osiris' left leg is a body part in Heliopolis-Marketplace.


Part 7 - Epilogue






